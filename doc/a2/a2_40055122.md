---
title: "Parser Design"
author: Tatum Alenko
date: February 19, 2019
geometry: margin=2cm
header-includes:
  - \usepackage{pdflscape}
  - \newcommand{\blandscape}{\begin{landscape}}
  - \newcommand{\elandscape}{\end{landscape}}
  - \usepackage{listings}
  - \usepackage{xcolor}
  - \definecolor{backcolour}{rgb}{0.96,0.96,0.96}
  - \lstdefinelanguage{none}{
    identifierstyle=
    }
  - \lstdefinestyle{codestyle}{
    basicstyle=\footnotesize,
    numberstyle=\tiny\color{mygray},
    language=none,
    framesep=8pt,
    frame=single,
    backgroundcolor=\color{backcolour}
    }
  - \lstset{style=codestyle}
  - \lstset{identifierstyle=\color{black}}
  - \usepackage{multicol}
  - \newcommand{\btwocol}{\begin{multicols}{2}}
  - \newcommand{\etwocol}{\end{multicols}}
  - \usepackage{float}
  - \let\origfigure\figure
  - \let\endorigfigure\endfigure
  - \renewenvironment{figure}[1][2] {
    \expandafter\origfigure\expandafter[H]
    } {
    \endorigfigure
    }
output: pdf_document
---

# Table of Contents

\tableofcontents

\newpage

# Section 1. Transformed Grammar Into LL(1)

## Steps to correcting first and follow set conflicts

Once the transformed grammar without EBNF optional/repeating constructs were removed along with left-recursion using Dr. Paquet's `grammartool.jar`, the resulting grammar output was entered in the UCalgary Grammar Tool. The result of the analysis of the grammar is shown in the following code block:

\begin{lstlisting}
FUNCHEAD has a first set conflict.
FUNCHEAD_1 is nullable with clashing first and follow sets.
MEMBERDECL has a first set conflict.
EXPR has a first set conflict.
FACTOR has a first set conflict.
FUNCTIONCALL_1 is nullable with clashing first and follow sets.
IDNEST has a first set conflict.
FUNCDECL has a first set conflict.
STATEMENT has a first set conflict.
VARIABLE_1 is nullable with clashing first and follow sets.
ARRAYSIZE has a first set conflict.
\end{lstlisting}

### `FUNCHEAD` and `FUNCHEAD_1`

Left factored the `FUNCHEAD_1 id lpar FPARAMS rpar colon ...` portion and changed the `FUNCHEAD_1` to take into that they both can start by `id` with optional `sr id` before the `lpar` terminal.

Before:

\begin{lstlisting}
FUNCHEAD -> FUNCHEAD_1 id lpar FPARAMS rpar colon TYPE .
FUNCHEAD -> FUNCHEAD_1 id lpar FPARAMS rpar colon void .

FUNCHEAD_1 -> id sr .
FUNCHEAD_1 -> .
\end{lstlisting}

After:

\begin{lstlisting}
FUNCHEAD -> id FUNCHEAD_1 lpar FPARAMS rpar colon TYPEORVOID .

FUNCHEAD_1 -> sr id .
FUNCHEAD_1 -> .

TYPEORVOID -> TYPE .
TYPEORVOID -> void .
\end{lstlisting}

### `FUNCDECL`

Left factored the `id lpar FARAMS rpar colon` portion and replaced the rule to account for `TYPEORVOID`:

Before:

\begin{lstlisting}
FUNCDECL -> id lpar FPARAMS rpar colon TYPE semi .
FUNCDECL -> id lpar FPARAMS rpar colon void semi .
\end{lstlisting}

After:

\begin{lstlisting}
FUNCDECL -> id lpar FPARAMS rpar colon TYPEORVOID semi .
\end{lstlisting}

### `ARRAYSIZE`

Factored the possibly nullable second `intnum` terminal.

Before:

\begin{lstlisting}
ARRAYSIZE -> lsqbr intnum rsqbr .
ARRAYSIZE -> lsqbr rsqbr .
\end{lstlisting}

After:

\begin{lstlisting}
ARRAYSIZE -> lsqbr MAYBEINTNUM rsqbr .

MAYBEINTNUM -> intnum .
MAYBEINTNUM -> .
\end{lstlisting}

### `IDNEST`

Left factored the `id` terminal.

Before:

\begin{lstlisting}
IDNEST -> id IDNEST_1 dot .
IDNEST -> id lpar APARAMS rpar dot .

IDNEST_1 -> INDEX IDNEST_1 .
IDNEST_1 -> .
\end{lstlisting}

After:

\begin{lstlisting}
IDNEST -> id IDNEST_1 .

IDNEST_1 -> IDNEST_2 dot .
IDNEST_1 -> lpar APARAMS rpar dot .

IDNEST_2 -> INDEX IDNEST_2 .
IDNEST_2 -> .
\end{lstlisting}

### `EXPR`

Left factored the common `ARITHEXPR` in both `RELEXPR` and `ARITHEXPR`

Before:

\begin{lstlisting}
EXPR -> ARITHEXPR .
EXPR -> RELEXPR .

ARITHEXPR -> TERM ARITHEXPR_1 .

ARITHEXPR_1 -> ADDOP TERM ARITHEXPR_1 .
ARITHEXPR_1 -> .

RELEXPR -> ARITHEXPR RELOP ARITHEXPR .
\end{lstlisting}

After:

\begin{lstlisting}
EXPR -> ARITHEXPR EXPR_1 .

EXPR_1 -> RELOP ARITHEXPR .
EXPR_1 -> .

ARITHEXPR -> TERM ARITHEXPR_1 .

ARITHEXPR_1 -> ADDOP TERM ARITHEXPR_1 .
ARITHEXPR_1 -> .

RELEXPR -> ARITHEXPR RELOP ARITHEXPR .
\end{lstlisting}

### `MEMBERDECL`

The first step was to factor the `FUNCDECL` to take care of `TYPE` or `void`. This was easily done by eliminating the two productions into one by using a seperate production `TYPEORVOID`. Afterwards, the `id` shared by both `FUNCDECL` and possibly `VARDECL` was slightly trickier because only when the `TYPE` of `VARDECL` uses the `id` terminal this would clash. To eliminate the clash, the `id` needed to be left factored out of both `FUNCDECL` and `VARDECL`. This was done with the help of `INTEGERORFLOAT` to take into account the case when `id` isn't the first terminal of `VARDECL`. This left `FUNCDECL` unsued now, so it was removed and the new production was renamed to `FUNCDECLIDLESS` to signifie that it used to be the old definition of `FUNCDECL` but is missing the first `id` token that was prepended.

After:

\begin{lstlisting}
MEMBERDECL -> FUNCDECL .
MEMBERDECL -> VARDECL .

FUNCDECL -> id lpar FPARAMS rpar colon TYPE semi .
FUNCDECL -> id lpar FPARAMS rpar colon void semi .

VARDECL -> TYPE id VARDECL_1 semi .

VARDECL_1 -> ARRAYSIZE VARDECL_1 .
VARDECL_1 -> .
\end{lstlisting}

Before:

\begin{lstlisting}
MEMBERDECL -> id MEMBERDECL_1 .
MEMBERDECL -> INTEGERORFLOAT VARDECL_2 .

MEMBERDECL_1 -> VARDECL_1 .
MEMBERDECL_1 -> FUNCDECLIDLESS .

FUNCDECLIDLESS -> lpar FPARAMS rpar colon TYPEORVOID semi .

VARDECL -> INTEGERORFLOAT VARDECL_1 .
VARDECL -> id VARDECL_1 .

VARDECL_1 -> id VARDECL_2 semi .

VARDECL_2 -> ARRAYSIZE VARDECL_2 .
VARDECL_2 -> .
\end{lstlisting}

### `FUNCTIONCALL_1`

The removal of the first and follow set clash for this one was tricky. At first, the clash was with respect to the `id` terminal. This was fixed by left factoring it. However, another first and follow set clash remained afterwards with respect to the `lpar` terminal. This was corrected by factoring the `IDNEST` components that were causing the issue by further decomposing it into `IDNEST_2` and `IDNEST_3`. This way, the conflicting `lpar` would not occur from a possibly null terminal.

Before:

\begin{lstlisting}
FUNCTIONCALL -> FUNCTIONCALL_1 id lpar APARAMS rpar .

FUNCTIONCALL_1 -> IDNEST FUNCTIONCALL_1 .
FUNCTIONCALL_1 -> .

IDNEST -> id IDNEST_1 dot .
IDNEST -> id lpar APARAMS rpar dot .

IDNEST_1 -> INDEX IDNEST_1 .
IDNEST_1 -> .
\end{lstlisting}

After:

\begin{lstlisting}
FUNCTIONCALL -> id FUNCTIONCALL_1 .

FUNCTIONCALL_1 -> IDNEST_1 FUNCTIONCALL_2 .

FUNCTIONCALL_2 -> dot id IDNEST_3 .
FUNCTIONCALL_2 -> .

IDNEST -> id IDNEST_1 dot .

IDNEST_1 -> IDNEST_2 .
IDNEST_1 -> IDNEST_3 .

IDNEST_2 -> INDEX IDNEST_2 .
IDNEST_2 -> .

IDNEST_3 -> lpar APARAMS rpar .
\end{lstlisting}

### `VARIABLE_1`

This first and follow set conflict required to factor the `id` terminal in `VARIABLE` that occured inside `IDNEST` (which was replaced by it's `IDNEST_1 id` form).

Before:

\begin{lstlisting}
VARIABLE -> VARIABLE_1 id VARIABLE_2 .

VARIABLE_1 -> IDNEST VARIABLE_1 .
VARIABLE_1 -> .

VARIABLE_2 -> INDEX VARIABLE_2 .
VARIABLE_2 -> .
\end{lstlisting}

After:

\begin{lstlisting}
VARIABLE -> id VARIABLE_1 VARIABLE_2 .

VARIABLE_1 -> IDNEST_1 dot VARIABLE_3 .

VARIABLE_2 -> INDEX VARIABLE_2 .
VARIABLE_2 -> .

VARIABLE_3 -> IDNEST VARIABLE_3 .
VARIABLE_3 -> .
\end{lstlisting}

### `FACTOR`

The first conflict for this was easy to fix with a simple left factorization of the `id` terminal shared between `VARIABLE` and `FUNCTIONCALL`. However, the new resulting production of this factorization introduced yet another first set conflict with respect to the `lsqbr` terminal. After fixing this one, yet another came along with regards to `id`, and another one after with regards to `IDNEST_3`, all of which were fixed using left factorization.

Before:

\begin{lstlisting}
FACTOR -> VARIABLE .
FACTOR -> FUNCTIONCALL .
FACTOR -> intnum .
FACTOR -> floatnum .
FACTOR -> lpar ARITHEXPR rpar .
FACTOR -> not FACTOR .
FACTOR -> SIGN FACTOR .

VARIABLE -> id VARIABLE_1 VARIABLE_2 .

VARIABLE_1 -> IDNEST_1 dot VARIABLE_3 .

VARIABLE_2 -> INDEX VARIABLE_2 .
VARIABLE_2 -> .

VARIABLE_3 -> IDNEST VARIABLE_3 .
VARIABLE_3 -> .

FUNCTIONCALL -> id FUNCTIONCALL_1 .

FUNCTIONCALL_1 -> IDNEST_1 FUNCTIONCALL_2 .

FUNCTIONCALL_2 -> dot id IDNEST_3 .
FUNCTIONCALL_2 -> .
\end{lstlisting}

After:

\begin{lstlisting}
FACTOR -> id FACTOR_1 .
FACTOR -> intnum .
FACTOR -> floatnum .
FACTOR -> lpar ARITHEXPR rpar .
FACTOR -> not FACTOR .
FACTOR -> SIGN FACTOR .

FACTOR_1 -> IDNEST_1 FACTOR_2 .

FACTOR_2 -> dot FACTOR_3 .
FACTOR_2 -> .

FACTOR_3 -> id FACTOR_4 .
FACTOR_3 -> .

FACTOR_4 -> IDNEST_2 dot VARIABLE_3 .
FACTOR_4 -> IDNEST_3 FACTOR_5 .

FACTOR_5 -> dot VARIABLE_3 .
FACTOR_5 -> .

VARIABLE -> id VARIABLE_1 VARIABLE_2 .

VARIABLE_1 -> IDNEST_1 VARIABLE_4 .

VARIABLE_2 -> INDEX VARIABLE_2 .
VARIABLE_2 -> .

VARIABLE_3 -> IDNEST VARIABLE_3 .
VARIABLE_3 -> .

VARIABLE_4 -> dot VARIABLE_3 .

FUNCTIONCALL -> id FUNCTIONCALL_1 .

FUNCTIONCALL_1 -> IDNEST_1 FUNCTIONCALL_2 .

FUNCTIONCALL_2 -> dot id IDNEST_3 .
FUNCTIONCALL_2 -> .
\end{lstlisting}

### `STATEMENT`

The initial conflict with this rule was `id` (corrected after left factoring and creating new production `STATEMENT_1`), which was factored out that involved the `ASSIGNSTAT` and `FUNCTIONCALL` variables. However, multiple new first set conflicts arose, such as: `IDNEST_1`, `dot`, and `id`, and `IDNEST_3` as made evident by the new productions `STATEMENT_2`, `STATEMENT_3`, `STATEMENT_4`, and `STATEMENT_5`, respectively. Because of the need for decomposing the rule so much, it ended up that the `FUNCTIONCALL`, `FUNCTIONCALL_1`, and `FUNCTIONCALL_2` productions were not needed anymore.

Before:

\begin{lstlisting}
STATEMENT -> ASSIGNSTAT semi .
STATEMENT -> if lpar RELEXPR rpar then STATBLOCK else STATBLOCK semi .
STATEMENT -> while lpar RELEXPR rpar STATBLOCK semi .
STATEMENT -> read lpar VARIABLE rpar semi .
STATEMENT -> write lpar EXPR rpar semi .
STATEMENT -> return lpar EXPR rpar semi .
STATEMENT -> FUNCTIONCALL semi .

ASSIGNSTAT -> VARIABLE ASSIGNOP EXPR .
\end{lstlisting}

After:

\begin{lstlisting}
STATEMENT -> id STATEMENT_1 .
STATEMENT -> if lpar RELEXPR rpar then STATBLOCK else STATBLOCK semi .
STATEMENT -> while lpar RELEXPR rpar STATBLOCK semi .
STATEMENT -> read lpar VARIABLE rpar semi .
STATEMENT -> write lpar EXPR rpar semi .
STATEMENT -> return lpar EXPR rpar semi .

STATEMENT_1 -> IDNEST_1 STATEMENT_2 .

STATEMENT_2 -> dot STATEMENT_3 .
STATEMENT_2 -> semi .

STATEMENT_3 -> id STATEMENT_4 .
STATEMENT_3 -> VARIABLE_2 ASSIGNSTAT_1 semi .

STATEMENT_4 -> IDNEST_2 dot VARIABLE_3 VARIABLE_2 ASSIGNSTAT_1 semi .
STATEMENT_4 -> IDNEST_3 STATEMENT_5 .

STATEMENT_5 -> dot VARIABLE_3 VARIABLE_2 ASSIGNSTAT_1 semi .
STATEMENT_5 -> semi .

ASSIGNSTAT -> VARIABLE ASSIGNSTAT_1 .

ASSIGNSTAT_1 -> ASSIGNOP EXPR .
\end{lstlisting}

## Transformed grammar after removal of EBNF optional/repeating notation and left-recursion

\lstinputlisting{doc/a2/assets/ebnffree.grm}

## Ambiguity-free grammar

\lstinputlisting{doc/a2/assets/ambiguityfree.grm}

# Section 2. First and Follow Sets

The first sets generated are shown in the following code block:

\lstinputlisting{doc/a2/assets/firstsets.grm}

The follow sets generated are shown in the following code block:

\lstinputlisting{doc/a2/assets/followsets.grm}

# Section 3. Design

The overall strategy to designing the parser involved a few steps:

- Implement a first and follow set generator based on a text file containing a DSL based grammar syntax
- Using the generated first and follow sets, implement a paser table generator
- Using the generated table parser, implement a table-driven parser capable of validating a given input file containing source code of the language defined by the grammar and generate a derivation table and list of syntax errors that result
- Add semantic action identifiers to the transformed grammar to direct the parser to perform abstract syntax tree construction steps

The error recovery strategy used was panic mode. Once an empty table cell is found, the implementation attempts to skip the tokens at fault until it finds one that resyncs with the current derivation and continues on parsing the next expressions. This was chosen for its simplicity, even though it does not always identify the true source of the problem in all cases.

Unfortunately, due to a lack time, the AST generation was not complete.

# Section 4. Use of Tools

The two main tools used for this assignment were Dr. Paquet's `grammartool.jar` and [UCalgary's Grammar Tool](http://smlweb.cpsc.ucalgary.ca). The `grammartool.jar` was used to quickly remove EBNF repeating/optional notations and left-recursion in the original grammar provided and output the resulting grammar in the notation compatible with the UCalgary Grammar Tool. By having a EBNF notation free grammar without left-recursion, the next step was to use the UCalgary Grammar Tool to iteratively attempt to solve all the ambiguities (as described in Section 1).

Other tools that were used were [GraphViz](https://www.graphviz.org) when generating the AST diagram.
